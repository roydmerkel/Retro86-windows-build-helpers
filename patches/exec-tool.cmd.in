@echo off
SET arg0=%0
SET arg1=%1
SET arg2=%2
SET args=%*

CALL :logging "exec-tool.cmd: %*" 1>&2


SET ORIGINAL_AS_FOR_TARGET=@WINE_ORIGINAL_AS_FOR_TARGET@
SET ORIGINAL_LD_FOR_TARGET=@WINE_ORIGINAL_LD_FOR_TARGET@
SET ORIGINAL_LD_BFD_FOR_TARGET=@WINE_ORIGINAL_LD_BFD_FOR_TARGET@
SET ORIGINAL_LD_GOLD_FOR_TARGET=@WINE_ORIGINAL_LD_GOLD_FOR_TARGET@
SET ORIGINAL_PLUGIN_LD_FOR_TARGET=@WINE_ORIGINAL_PLUGIN_LD_FOR_TARGET@
SET ORIGINAL_NM_FOR_TARGET=@WINE_ORIGINAL_NM_FOR_TARGET@
SET ORIGINAL_DSYMUTIL_FOR_TARGET=@WINE_ORIGINAL_DSYMUTIL_FOR_TARGET@
SET exeext=@host_exeext@
SET fast_install=@enable_fast_install@
SET objdir=@objdir@

SET ORIGINAL_AS_FOR_TARGET=%ORIGINAL_AS_FOR_TARGET:/=\%
SET ORIGINAL_LD_FOR_TARGET=%ORIGINAL_LD_FOR_TARGET:/=\%
SET ORIGINAL_LD_BFD_FOR_TARGET=%ORIGINAL_LD_BFD_FOR_TARGET:/=\%
SET ORIGINAL_LD_GOLD_FOR_TARGET=%ORIGINAL_LD_GOLD_FOR_TARGET:/=\%
SET ORIGINAL_PLUGIN_LD_FOR_TARGET=%ORIGINAL_PLUGIN_LD_FOR_TARGET:/=\%
SET ORIGINAL_NM_FOR_TARGET=%ORIGINAL_NM_FOR_TARGET:/=\%
SET ORIGINAL_DSYMUTIL_FOR_TARGET=%ORIGINAL_DSYMUTIL_FOR_TARGET:/=\%

REM echo exec-tool.cmd: PATH: "%PATH%"
REM echo "environment: "
REM set

SET current=%cd%
pushd %current%
pushd ..
SET parentdir=%cd%
popd
popd

SET invoked=%arg0%
SET invokeddir=%arg0%
for /F %%i in ("%invoked%") do SET invoked=%%~ni
for /F %%i in ("%invokeddir%") do SET invokeddir=%%~dpi

SET id=%invoked%

IF x%invoked%==xas goto invas
IF x%invoked%==xcollect-ld goto invcollectld
IF x%invoked%==xnm goto invnm
IF x%invoked%==xdsymutil goto invdsymutil

goto end

:invas
SET original=%ORIGINAL_AS_FOR_TARGET%
SET prog=as-new%exeext%
SET dir=gas
goto endselect

:invcollectld
REM Check -fuse-ld=bfd and -fuse-ld=gold

echo %args% | wineFindStrWorkarround "-fuse-ld=bfd"

IF %ERRORLEVEL% EQU 0 goto bfdld

echo %args% | wineFindStrWorkarround "-fuse-ld=gold"

if %ERRORLEVEL% EQU 0 goto goldld

if x%arg1%==x-plugin goto pluginld

if x%arg2%==x-plugin goto pluginld

SET original=%ORIGINAL_LD_FOR_TARGET%
goto choseld

:bfdld
SET original=%ORIGINAL_LD_BFD_FOR_TARGET%
goto choseld

:goldld
SET original=%ORIGINAL_LD_GOLD_FOR_TARGET%
goto choseld

:pluginld
SET original=%ORIGINAL_PLUGIN_LD_FOR_TARGET%
goto choseld

:choseld
SET prog=ld-new%exeext%

SET testorig=%original%
FOR /F %%i IN ("%testorig%") DO SET testorigext=%%~xi
CALL SET testorig=%%testorig:%testorigext%=%%

if x%testorig% == x..\gold\ld-new goto golddir
if x%testorig% == x%parentdir%\gold\ld-new goto golddir

SET dir=ld
goto chosedir

:golddir
SET dir=gold
REM No need to handle relink since gold doesn't use libtool.
SET fast_install=yes

goto chosedir

:chosedir

SET id=ld

goto endselect

:invnm
SET original=%ORIGINAL_NM_FOR_TARGET%
SET prog=nm-new%exeext%
SET dir=binutils
goto endselect

:invdsymutil
SET original=%ORIGINAL_DSYMUTIL_FOR_TARGET%
REM We do not build this in tree - but still want to be able to execute
REM a configured version from the build dir.
SET prog=
SET dir=
goto endselect

:endselect

if x%original:~0,3%==x..\ goto origdotdot

CALL SET originalreplacedotdot=%%original:%parentdir%\=%%
echo %originalreplacedotdot% | wineFindStrWorkarround "\"
if %ERRORLEVEL% EQU 0 goto origdotdot

REM echo exec-tool.cmd: call %original% %args%
CALL :logging "call %original% %args%" 1>&2
call %original% %args%
goto end

:origdotdot
REM compute absolute path of the location of this script
SET tdir=%invokeddir%
pushd %tdir%
SET scriptdir=%cd%
popd

if exist %scriptdir%\..\%dir%\%prog% goto testxscrdirprg
if exist %scriptdir%\..\%dir%\%prog%.exe goto testxscrdirprg
if exist %scriptdir%\..\%dir%\%prog%.cmd goto testxscrdirprg
if exist %scriptdir%\..\%dir%\%prog%.bat goto testxscrdirprg

REM echo exec-tool.cmd: call %scriptdir%\..\prev-%dir%\%prog% %args%
CALL :logging "call %scriptdir%\..\prev-%dir%\%prog% %args%" 1>&2
call %scriptdir%\..\prev-%dir%\%prog% %args%
goto end

:testxscrdirprg

REM if NOT x%fast_install%==xyes echo exec-tool.cmd: call %scriptdir%\..\%dir%\%prog% %args%
if NOT x%fast_install%==xyes CALL :logging "call %scriptdir%\..\%dir%\%prog% %args%" 1>&2
if NOT x%fast_install%==xyes call %scriptdir%\..\%dir%\%prog% %args%
if NOT x%fast_install%==xyes goto end

REM if libtool did everything it needs to do, there's a fast path
SET lt_prog=%scriptdir%\..\%dir%\%objdir%\lt-%prog%

REM if exist %lt_prog%.exe echo exec-tool.cmd: %lt_prog%.exe %args%
if exist %lt_prog%.exe CALL :logging "%lt_prog%.exe %args%" 1>&2
if exist %lt_prog%.exe %lt_prog%.exe %args%
if exist %lt_prog%.exe goto end
REM if exist %lt_prog%.cmd echo exec-tool.cmd: call %lt_prog%.cmd %args%
if exist %lt_prog%.cmd CALL :logging "call %lt_prog%.cmd %args%" 1>&2
if exist %lt_prog%.cmd call %lt_prog%.cmd %args%
if exist %lt_prog%.cmd goto end
REM if exist %lt_prog%.cmd echo exec-tool.cmd: call %lt_prog%.bat %args%
if exist %lt_prog%.bat CALL :logging "call %lt_prog%.bat %args%" 1>&2
if exist %lt_prog%.bat call %lt_prog%.bat %args%
if exist %lt_prog%.bat goto end
REM if exist %lt_prog% echo exec-tool.cmd: call %lt_prog% %args%
if exist %lt_prog%.exe CALL :logging "call %lt_prog% %args%" 1>&2
if exist %lt_prog% call %lt_prog% %args%
if exist %lt_prog% goto end

REM libtool has not relinked ld-new yet, but we cannot just use the
REM previous stage (because then the relinking would just never happen!).
REM So we take extra care to use prev-ld/ld-new *on recursive calls*.
CALL SET LT_RCU=%%LT_RCU_%id%%%
REM if x%LT_RCU%==x1 echo exec-tool.cmd: call %scriptdir%\..\prev-%dir%\%prog% %args%
if x%LT_RCU%==x1 CALL :logging "call %scriptdir%\..\prev-%dir%\%prog% %args%" 1>&2
if x%LT_RCU%==x1 call %scriptdir%\..\prev-%dir%\%prog% %args%
if x%LT_RCU%==x1 goto end

CALL SET LT_RCU_%id%=1
REM echo exec-tool.cmd: call %scriptdir%\..\%dir%\%prog% %args%
CALL :logging "call %scriptdir%\..\%dir%\%prog% %args%" 1>&2
call %scriptdir%\..\%dir%\%prog% %args%
exit /b %errorlevel%

:end
goto end2

:logging
ECHO exec-tool.cmd: %*

:end2
